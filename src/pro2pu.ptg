/** **/
Package: "package \"" [PtgOutId $1 int]  "\" {" [IndentIncr] [IndentNewLine] $2 [IndentDecr] [IndentNewLine] "}"
Class: "class " $1 " {" [IndentIncr] [IndentNewLine] $2 [IndentDecr] [IndentNewLine] "}"
Enum: "enum " $1 " {"  [IndentIncr] [IndentNewLine] $2 [IndentDecr] [IndentNewLine] "}"

NoPackage: "package \"\" {" [IndentIncr] [IndentNewLine] $1 [IndentDecr] [IndentNewLine] "}"

/* e.g. {field} +optional type : PhoneType */
Field: "{field} +" $3 " " [PtgOutId $1 int] " : " [PtgOutId $2 int]
FieldWithDefault: "{field} +" $3 " " [PtgOutId $1 int] " : " [PtgOutId $2 int] {" (default="} [PtgOutId $4 int] {")"}

OneOf: ".. oneof " [PtgOutId $1 int] " .." [IndentNewLine] $2 [IndentNewLine] ".." [IndentNewLine]

/* e.g. {field} +type : map<int, Person> */
MapField: "{field} +" [PtgOutId $1 int] " : map<" [PtgOutId $2 int] ":" [PtgOutId $3 int] ">"

/* e.g. MOBILE */
EnumItem: [PtgOutId $1 int]

/* e.g. AddressBook o-- Person : repeated, formerly: PTGLink */
RelationUsing: $1 " o-- " [PtgOutId $2 int] " : " $3

/* Newline sequence with optional arguments */
NlSeq: $1 {[IndentNewLine]} $2

DotSeq: $1 {"."} $2

PUML: "@startuml" [IndentNewLine] $ [IndentNewLine] "@enduml" [IndentNewLine]

oneofVariant: [PtgOutId $1 int ] " variant"